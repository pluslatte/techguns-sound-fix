buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "jitpack"
            url = "https://jitpack.io"
        }
    }
    dependencies {
        classpath ('com.anatawa12.forge:ForgeGradle:1.2-1.1.+') {
            changing = true
        }
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
}

apply plugin: 'forge'

// These settings allow you to choose what version of Java you want to be compatible with. Forge 1.7.10 runs on Java 6 to 8.
sourceCompatibility = 1.6
targetCompatibility = 1.6

version = project.version
group = project.group

minecraft {
    version = project.minecraft_version + "-" + project.forge_version
    runDir = "run"
}

repositories {
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
}

dependencies {
    // Techguns mod (compile時のみ必要、実行時はmodsフォルダから読み込まれる)
    compileOnly files('libs/Techguns.beta.1.2_alphatest4.1.jar')
    
    // UniMixins dependencies for Mixin support
    def unimixinsVersion = "0.2.1"
    implementation("com.github.LegacyModdingMC.UniMixins:unimixins-all-1.7.10:${unimixinsVersion}:dev") {
        transitive = false
    }
    
    // Add annotation processor for Mixin refmap generation
    annotationProcessor "org.ow2.asm:asm-debug-all:5.0.3"
    annotationProcessor "com.google.guava:guava:24.1.1-jre"
    annotationProcessor "com.google.code.gson:gson:2.8.6"
    annotationProcessor("com.github.LegacyModdingMC.UniMixins:unimixins-all-1.7.10:${unimixinsVersion}:dev") {
        transitive = false
    }
}

processResources {
    // This will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // Replace values in only mcmod.info.
    filesMatching('mcmod.info') {
        // Replace version and mcversion.
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
}

// Add manifest to jar
jar {
    manifest {
        attributes 'FMLCorePlugin': 'com.pluslatte.techgunssoundfix.mixins.TechgunsSoundFixMixinPlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

// Ensures that the encoding of source files is set to UTF-8, see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    // Configure Mixin annotation processor
    options.compilerArgs += [
        "-AreobfSrgFile=${tasks.reobf.srg}",
        "-AoutSrgFile=${project.buildDir}/tmp/mixins.srg",
        "-AoutRefMapFile=${project.buildDir}/resources/main/mixins.techgunssoundfix.refmap.json"
    ]
}

// This task creates a .jar file containing the source code of this mod.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

// This task creates a .jar file containing a deobfuscated version of this mod, for other developers to use in a development environment.
task devJar(type: Jar) {
    classifier = "dev"
    from sourceSets.main.output
}

// Creates the listed artifacts on building the mod.
artifacts {
    archives sourcesJar
    archives devJar
}

// This block configures any maven publications you want to make.
publishing {
    publications {
        mavenJava(MavenPublication) {
        // Add any other artifacts here that you would like to publish!
            artifact(jar) {
                builtBy build
            }
            artifact(sourcesJar) {
                builtBy sourcesJar
            }
            artifact(devJar) {
                builtBy devJar
            }
        }
    }

    // This block selects the repositories you want to publish to.
    repositories {
        // Add the repositories you want to publish to here.
    }
}
